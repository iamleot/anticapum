#!/usr/pkg/bin/perl

use strict;
use warnings;
use HTTP::Tiny;
use Term::ReadKey;

use constant {
	ANTICAPUM_URI => "https://captive.diiga.univpm.it/login.php",
	ANTICAPUM_SLEEP => 310,
};


#
# Get credential and return them via an array list (username, password).
#
sub
credential()
{
	my ($username, $password);

	print "username: ";
	$username = <>;
	print "password: ";
	ReadMode "noecho";
	$password = <>;
	ReadMode "restore";
	print "\n";
	chomp($username, $password);

	return ($username, $password);
}


#
# Properly login through the form given a username and password.
# die() in case of insuccess.
#
sub
login($$)
{
	my ($user, $pass) = @_;
	my ($auth, $ht, $res, $url);

	$ht = HTTP::Tiny->new();

	$url = ANTICAPUM_URI;
	$auth = {
		username => $user,
		password => $pass,
	};

	$res = $ht->post_form($url, $auth);

	if (!$res->{success}) {
		die "$0: could not login\n";
	}
}


my ($pass, $user);
($user, $pass) = credential();

for (;;) {
	login($user, $pass);
	sleep(ANTICAPUM_SLEEP);
}
